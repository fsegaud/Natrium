graphics_init:
    ld $scr_w $d_scr_w
    ld $scr_h $d_scr_h
    j  ra

graphics_draw:
    push ra
    sd   $d_scr_clr 0 ; clear screen
    jal  graphics_draw_borders
    jal  graphics_draw_player
    jal  graphics_draw_enemy
    jal  graphics_draw_seed
    jal  graphics_draw_score
    pop  ra
    j    ra
; graphics_draw

graphics_draw_borders:
    mov     $i        0
    mov     $stop     $scr_w
    sd      $d_scr_a  1
    sd      $d_scr_i  0
    sd      $d_scr_c  0xc0 ; gray bg
    graphics_draw_borders_upper:
        sd  $d_scr_v 0x20
        inc $i
        blt graphics_draw_borders_upper $i $stop               
    
    mul     $stop     $scr_h $scr_w
    sub     $i        $stop  $scr_w
    sd      $d_scr_i  $i
    sd      $d_scr_c  0xc0 ; gray bg
    graphics_draw_borders_lower:
        sd  $d_scr_v 0x20
        inc $i
        blt graphics_draw_borders_lower $i $stop   
    
    mov     $i        0
    mul     $stop     $scr_h $scr_w
    sub     $stop     $stop  $scr_w
    sd      $d_scr_a  0
    graphics_draw_borders_left:
        sd  $d_scr_i $i
    sd      $d_scr_c  0xc0 ; gray bg
        sd  $d_scr_v 0x20
        add $i        $i     $scr_w
        blt graphics_draw_borders_left $i $stop 
    
    mov     $i        $scr_w
    dec     $i
    mul     $stop     $scr_h $scr_w
    dec     $stop
    graphics_draw_borders_right:
        sd  $d_scr_i $i
        sd  $d_scr_c  0xc0 ; gray bg
        sd  $d_scr_v 0x20
        add $i        $i     $scr_w
        blt graphics_draw_borders_right $i $stop
    j ra
; graphics_draw_borders

graphics_draw_player:
    mul $i  $py   $scr_w
    add $i  $i    $px
    sd  $d_scr_i $i
    sd  $d_scr_c 0x30 ; blue bg 
    sd  $d_scr_v $GRAPH_PLAYER
    j ra
; graphics_draw_player

graphics_draw_enemy:
    mul $i  $ey   $scr_w
    add $i  $i    $ex
    sd  $d_scr_i $i
    sd  $d_scr_c 0x01 ; red 
    sd  $d_scr_v $GRAPH_ENEMY
    j ra
; graphics_draw_enemy

graphics_draw_seed:
    mul $i  $sy   $scr_w
    add $i  $i    $sx
    sd  $d_scr_i $i
    sd  $d_scr_c 0x2 ; green
    sd  $d_scr_v $GRAPH_SEED
    j ra
; graphics_draw_seed

graphics_draw_score:
    push ra
       
    ; score label
    sd  $d_scr_a 1   
    sd  $d_scr_i 0
    sd  $d_scr_c 0xca ; black on white
    sd  $d_scr_v 0x53 ; S
    sd  $d_scr_v 0x63 ; c
    sd  $d_scr_v 0x6f ; o
    sd  $d_scr_v 0x72 ; r
    sd  $d_scr_v 0x65 ; e
    sd  $d_scr_v 0x3a ; :
    sd  $d_scr_v 0x30 ; 0
    sd  $d_scr_v 0x30 ; 0
    sd  $d_scr_v 0x30 ; 0
    sd  $d_scr_a 0
    
    ; score value
    mov $i 8
    mov r4 $score
    graphics_draw_score_loop:
        mod r5 r4 10   ; TODO: floor/trunc r5
        add r5 r5 0x30 ; ascii
        sd  $d_scr_i $i
        sd  $d_scr_v r5
        div r4 r4 10
        dec $i
        bgte graphics_draw_score_loop r4 1
    
    pop ra
    j ra
; graphics_draw_score

